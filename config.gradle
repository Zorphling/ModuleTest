project.ext {

    isModuleDebug = false

    isFlavors = true
    applicationId = "com.yes.project.moduletest"
    compileSdk = 31
    minSdk = 21
    targetSdk = 31
    versionCode = 1
    versionName = "1.0"

    setAppDefaultConfig = {
        Project project ->
            if (project.name == 'app') {
                project.apply plugin: 'com.android.application'
            } else {
                if (isModuleDebug) {
                    project.apply plugin: 'com.android.application'
                } else {
                    project.apply plugin: 'com.android.library'
                }
            }
            project.apply plugin: 'kotlin-android'
            project.apply plugin: 'kotlin-kapt'
            setAndroidConfig(project.android)
            setDependencies(project.dependencies)
    }

    setAndroidConfig = {
        android ->
            /**
             * 签名配置
             */
            android.signingConfigs {
                debug {
                    storeFile file("${rootProject.projectDir}/23youhui.jks")
                    storePassword '23youhuikey123'
                    keyAlias '23youhui'
                    keyPassword '23youhuikey123'
                    v1SigningEnabled true
                    v2SigningEnabled true
                }
                release {
                    storeFile file("${rootProject.projectDir}/23youhui.jks")
                    storePassword '23youhuikey123'
                    keyAlias '23youhui'
                    keyPassword '23youhuikey123'
                    v1SigningEnabled true
                    v2SigningEnabled true
                }
            }

            /**
             * SDK版本配置
             */
            android.compileSdkVersion this.compileSdk
            android.defaultConfig {
                if (project.name == 'app') {
                    applicationId project.applicationId
                } else if (isModuleDebug) {
                    applicationId project.applicationId
                    applicationIdSuffix = project.name
                }

                versionCode this.versionCode
                versionName this.versionName
                // 最低安装版本
                minSdkVersion this.minSdk
                // 目标适配版本
                targetSdkVersion this.targetSdk

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                ndk {
                    // armeabi：万金油架构平台（占用率：0%）
                    // armeabi-v7a：曾经主流的架构平台（占用率：10%）
                    // arm64-v8a：目前主流架构平台（占用率：95%）
                    abiFilters 'armeabi-v7a', 'arm64-v8a', 'armeabi'
                }

                javaCompileOptions {
                    kapt {
                        arguments {
                            arg("AROUTER_MODULE_NAME", project.getName())
                        }
//                        arguments += [AROUTER_MODULE_NAME: project.getName()]
                    }
                }

                manifestPlaceholders = rootProject.ext.sdkConfig

                android.flavorDimensions "channal_name"

            }
            android.resourcePrefix "${project.name}_"
            android.productFlavors {
                if (isFlavors) {
                    yingyongbao {
                        dimension "channal_name"
                    }
                    wandoujia {}
                    baidu {}
                    c360 {}
                    uc {}
                    xiaomi {}
                    vivo {}
                    oppo {}
                    huawei {}
                    c91zhushou {}
                    meizu {}
                    a23youhui {}
                    android.productFlavors.all { flavor ->
                        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name, JPUSH_CHANNEL: name]
                    }
                }
            }

            android.buildTypes {
                debug {

                    signingConfig android.signingConfigs.debug
                    buildConfigField "Boolean", "IS_DEBUG", "true"
                    minifyEnabled false

                }
                release {
                    signingConfig android.signingConfigs.release
                    buildConfigField "Boolean", "IS_DEBUG", "false"
                    minifyEnabled true
                    proguardFiles android.getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }
            android.lintOptions {
                disable "Instantiatable"
            }
            android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            android.kotlinOptions {
                jvmTarget = '1.8'
            }

            android.sourceSets {
                main {
                    if (project.name != "app") {
                        if (isModuleDebug) {
                            manifest.srcFile "src/main/debug/AndroidManifest.xml"
                        } else {
                            manifest.srcFile "src/main/AndroidManifest.xml"
                        }
                    }
                    java {
                        exclude "module/**"
                    }
                }
            }
    }

    setDependencies = {
        dependencies ->
            libs.each {
                k, v ->
                    dependencies.implementation v
            }
            apts.each {
                k, v ->
                    dependencies.kapt v
            }
            dependencies.testImplementation 'junit:junit:4.+'
            dependencies.androidTestImplementation 'androidx.test.ext:junit:1.1.3'
            dependencies.androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

            if (project.name == "app" && !isModuleDebug) {
                dependencies.implementation project(":common:base")
                dependencies.implementation project(":common:module")
            }
    }
}
setAppDefaultConfig(project)